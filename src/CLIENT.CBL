       IDENTIFICATION DIVISION.
       PROGRAM-ID. CLIENT.

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       REPOSITORY. FUNCTION ALL INTRINSIC.

       DATA DIVISION.

       WORKING-STORAGE SECTION.
       01  ERRNO BINARY-CHAR UNSIGNED.
       01  ERRNO-NAME PIC X(16).
       01  ERRNO-MESSAGE PIC X(64).

       01  GENERAL-MESSAGE PIC X(128).

       01 SOCKET-DESCRIPTOR BINARY-INT.
       01 SOCKET-DESCRIPTOR-RESPONSE BINARY-INT.
       01 RECEIVE-ADDRESS.
          03  RECEIVE-FAMILY BINARY-SHORT.
          03  RECEIVE-PORT BINARY-SHORT UNSIGNED.
          03  RECEIVE-IP-ADDRESS BINARY-INT.
          03  FILLER PIC X(8) VALUE LOW-VALUES.

       01 AF_INET BINARY-INT VALUE 2.
       01 SOCK_DGRAM BINARY-INT VALUE 2.
       01 SOL_SOCKET BINARY-INT VALUE 1.
       01 SO_REUSEADDR BINARY-INT VALUE 2.
       01 YES BINARY-INT VALUE 1.
       01 ZERO-VALUE BINARY-INT VALUE 0.

       01 MESSAGE-V PIC X(80).
          88 MESSAGE-QUIT-RECIEVED VALUE "QUIT".
       01 MESSAGE-LENGTH BINARY-SHORT UNSIGNED.
       01 STRUCT-LENGTH BINARY-SHORT UNSIGNED.

       01  ADDRESS-HOST PIC X(128).
       01  ADDRESS-HOST-SERVICE PIC X(32).

       01 ADDRESS-HINTS.
          03  ADDRESS-HINTS-FLAGS BINARY-INT SYNC.
          03  ADDRESS-HINTS-FAMILY BINARY-INT SYNC.
          03  ADDRESS-HINTS-SOCKTYPE BINARY-INT SYNC.
          03  ADDRESS-HINTS-PROTOCOL BINARY-INT SYNC.
          03  ADDRESS-HINTS-ADDRESS-LENGTH BINARY-INT SYNC.
          03  ADDRESS-HINTS-ADDRESS POINTER SYNC.
          03  ADDRESS-HINTS-CANONNAME POINTER SYNC.
          03  ADDRESS-HINTS-NEXT POINTER.
       01 GETADDRINFO-RESULT POINTER.

       01 ADDRESS-INFO BASED.
          03  ADDRESS-INFO-FLAGS BINARY-INT SYNC.
          03  ADDRESS-INFO-FAMILY BINARY-INT SYNC.
          03  ADDRESS-INFO-SOCKTYPE BINARY-INT SYNC.
          03  ADDRESS-INFO-PROTOCOL BINARY-INT SYNC.
          03  ADDRESS-INFO-ADDRESS-LENGTH BINARY-INT SYNC.
          03  ADDRESS-INFO-ADDRESS POINTER SYNC.
          03  ADDRESS-INFO-CANONNAME POINTER SYNC.
          03  ADDRESS-INFO-NEXT POINTER.

       01 ABORT-MESSAGE PIC X(64).

       01 COMMAND-MESSAGE PIC X(80) VALUE SPACES.

       01 COMMAND-PARAMETERS PIC X(128).
       01 INPUT-PARAMETERS.
           05 SERVER-NAME PIC X(64) VALUE SPACES.
           05 SERVER-PORT  PIC X(16) VALUE SPACES.
           05 SERVER-PORT-BINARY BINARY-SHORT UNSIGNED.
           05 BOT-NAME PIC X(32) VALUE SPACES.
           05 BOT-PGM-NAME PIC X(08) VALUE SPACES.

       01 BOT-PARAMETERS.
          COPY DATA.

       PROCEDURE DIVISION.
        ACCEPT COMMAND-PARAMETERS FROM COMMAND-LINE END-ACCEPT
        UNSTRING COMMAND-PARAMETERS DELIMITED BY ALL SPACES
            INTO SERVER-NAME SERVER-PORT BOT-NAME BOT-PGM-NAME
        END-UNSTRING

        CALL 'socket' USING
            BY VALUE AF_INET
            BY VALUE SOCK_DGRAM
            BY VALUE 0
            GIVING SOCKET-DESCRIPTOR
        END-CALL

        IF RETURN-CODE = -1
          MOVE "MESSAGERECEIVE CALL 'SOCKET' FAILED" TO ABORT-MESSAGE
          PERFORM ABORT-RECEIVE
        END-IF

        CALL 'setsockopt' USING
            BY VALUE SOCKET-DESCRIPTOR
            BY VALUE SOL_SOCKET
            BY VALUE SO_REUSEADDR
            BY REFERENCE YES
            BY VALUE LENGTH(YES)
        END-CALL

        IF RETURN-CODE = -1
          MOVE "MESSAGERECEIVE CALL 'SETSOCKOPT' FAILED"
           TO ABORT-MESSAGE
           PERFORM ABORT-RECEIVE
        END-IF

        COMPUTE RECEIVE-FAMILY = AF_INET END-COMPUTE

      * Call bind
        CALL 'bind' USING
          BY VALUE SOCKET-DESCRIPTOR
          BY REFERENCE RECEIVE-ADDRESS
          BY VALUE LENGTH(RECEIVE-ADDRESS)
        END-CALL

        CALL 'getsockname' USING
          BY VALUE SOCKET-DESCRIPTOR
          BY REFERENCE RECEIVE-ADDRESS
          BY REFERENCE LENGTH(RECEIVE-ADDRESS)
          GIVING SOCKET-DESCRIPTOR-RESPONSE
        END-CALL

        IF RETURN-CODE = -1
          MOVE "getsockname call FAILED"
            TO ABORT-MESSAGE
          PERFORM ABORT-RECEIVE
        END-IF

        PERFORM AUFLOESEN-SERVERADRESSE

        PERFORM SEND-REGISTER

        PERFORM WITH TEST AFTER UNTIL MESSAGE-QUIT-RECIEVED
          MOVE SPACES TO MESSAGE-V
          CALL 'recv' USING
             BY VALUE SOCKET-DESCRIPTOR
             BY REFERENCE MESSAGE-V
             BY VALUE LENGTH(MESSAGE-V)
             BY VALUE 0
          END-CALL
          IF RETURN-CODE = -1
            MOVE "messagereceive call 'recv' failed" TO ABORT-MESSAGE
            PERFORM ABORT-RECEIVE
          END-IF
          COMPUTE MESSAGE-LENGTH = RETURN-CODE END-COMPUTE
          IF MESSAGE-LENGTH = 0
            MOVE 'empty message' TO MESSAGE-V
            MOVE 13 TO MESSAGE-LENGTH
          END-IF

          MOVE MESSAGE-V
            TO SERVER-MESSAGE IN BOT-PARAMETERS
          CALL BOT-PGM-NAME USING BOT-PARAMETERS
          PERFORM SEND-REPLY-MESSAGE-TO-SERVER
        END-PERFORM

        CALL 'CLOSE' USING BY VALUE SOCKET-DESCRIPTOR END-CALL

        STOP RUN
       .

       SEND-REGISTER SECTION.
           STRING "REGISTER;" DELIMITED BY SIZE
                  BOT-NAME    DELIMITED BY SPACE
             INTO COMMAND-MESSAGE

           PERFORM SEND-MESSAGE-TO-SERVER
       EXIT.

       SEND-REPLY-MESSAGE-TO-SERVER SECTION.
           IF MESSAGE-TO-SERVER NOT = SPACES THEN
              MOVE MESSAGE-TO-SERVER
                TO COMMAND-MESSAGE
              PERFORM SEND-MESSAGE-TO-SERVER
           END-IF
       EXIT.

       SEND-MESSAGE-TO-SERVER SECTION.
           PERFORM VARYING MESSAGE-LENGTH FROM 1 BY 1
             UNTIL MESSAGE-LENGTH > LENGTH(COMMAND-MESSAGE)
             OR COMMAND-MESSAGE(MESSAGE-LENGTH:) = SPACE
              CONTINUE
           END-PERFORM
           SUBTRACT 1 FROM MESSAGE-LENGTH END-SUBTRACT

           CALL 'sendto' USING
            BY VALUE SOCKET-DESCRIPTOR
            BY REFERENCE COMMAND-MESSAGE
            BY VALUE MESSAGE-LENGTH
            BY VALUE ZERO-VALUE
            BY VALUE ADDRESS-INFO-ADDRESS
            BY VALUE ADDRESS-INFO-ADDRESS-LENGTH
           END-CALL

           IF RETURN-CODE = -1
             MOVE "'send message' call failed" TO ABORT-MESSAGE
             PERFORM ABORT-RECEIVE
           END-IF
       EXIT.
       AUFLOESEN-SERVERADRESSE SECTION.
           INITIALIZE ADDRESS-HINTS
           MOVE AF_INET TO ADDRESS-HINTS-FAMILY
           MOVE SOCK_DGRAM TO ADDRESS-HINTS-SOCKTYPE

           MOVE SPACES TO ADDRESS-HOST
           STRING SERVER-NAME DELIMITED BY SPACE
            X'00' DELIMITED BY SIZE
            INTO ADDRESS-HOST
           END-STRING
           MOVE SPACES TO ADDRESS-HOST-SERVICE
           STRING SERVER-PORT DELIMITED BY SPACE
            X'00' DELIMITED BY SIZE
            INTO ADDRESS-HOST-SERVICE
           END-STRING

           CALL 'getaddrinfo' USING
            BY CONTENT ADDRESS-HOST
            BY CONTENT ADDRESS-HOST-SERVICE
            BY REFERENCE ADDRESS-HINTS
            BY REFERENCE GETADDRINFO-RESULT
           END-CALL
           IF RETURN-CODE = -1
            MOVE "getaddrinfo call failed" TO ABORT-MESSAGE
            PERFORM ABORT-RECEIVE
           END-IF
           SET ADDRESS OF ADDRESS-INFO TO GETADDRINFO-RESULT
       EXIT.

       ABORT-RECEIVE SECTION.
        DISPLAY ABORT-MESSAGE
        STOP RUN
       .
       END PROGRAM CLIENT.
